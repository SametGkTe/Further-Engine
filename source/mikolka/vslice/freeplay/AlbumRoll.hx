package mikolka.vslice.freeplay;

import mikolka.funkin.custom.mobile.MobileScaleMode;
import mikolka.funkin.freeplay.album.AlbumRegistry;
import mikolka.funkin.freeplay.album.Album;
import flixel.FlxSprite;
import flixel.group.FlxSpriteGroup;
import flixel.util.FlxSort;
import flixel.tweens.FlxTween;
import flixel.util.FlxTimer;
import mikolka.compatibility.funkin.FunkinPath as Paths;

class AlbumRoll extends FlxSpriteGroup
{
  public var albumId(default, set):Null<String>;

  function set_albumId(value:Null<String>):Null<String>
  {
    if (this.albumId != value)
    {
      this.albumId = value;
      updateAlbum();
    }

    return value;
  }

  var newAlbumArt:FlxAtlasSprite;
  var albumTitle:FunkinSprite;

  var difficultyStars:DifficultyStars;
  var _exitMovers:Null<FreeplayState.ExitMoverData>;
  var _exitMoversCharSel:Null<FreeplayState.ExitMoverData>;

  var albumData:Album;

  public function new()
  {
    super();

    newAlbumArt = new FlxAtlasSprite((FlxG.width - 640) - MobileScaleMode.gameNotchSize.x, 360, "freeplay/albumRoll/freeplayAlbum");
    newAlbumArt.visible = false;
    newAlbumArt.onAnimationComplete.add(onAlbumFinish);

    add(newAlbumArt);

    difficultyStars = new DifficultyStars((FlxG.width - 1140) - MobileScaleMode.gameNotchSize.x, 39);
    difficultyStars.visible = false;
    add(difficultyStars);

    buildAlbumTitle("freeplay/albumRoll/volume1-text");
    albumTitle.visible = false;

     newAlbumArt.onAnimationComplete.add(onAlbumFinish);
  }

  function onAlbumFinish(animName:String):Void
  {
    if (animName != "idle")
    {
      newAlbumArt.playAnimation('idle', true);
    }
  }

  function updateAlbum():Void
  {
    if (albumId == null)
    {
      this.visible = false;
      difficultyStars.stars.visible = false;
      return;
    }
    else
    {
      this.visible = true;
    }

    albumData = AlbumRegistry.instance.fetchEntry(albumId);

    if (albumData == null || !Paths.exists("images/"+albumData.getAlbumArtAssetKey()+".png")) 
    {
      if(albumId != ''){
        FlxG.log.warn('Could not find album data for album ID: ${albumId}');
        trace('Could not find album data for album ID: ${albumId}');
      }

      this.visible = false;
      difficultyStars.stars.visible = false;
      return;
    };

    var albumGraphic = Paths.noGpuImage(albumData.getAlbumArtAssetKey());
    newAlbumArt.replaceFrameGraphic(0, albumGraphic);

    buildAlbumTitle(albumData.getAlbumTitleAssetKey());

    applyExitMovers();

    refresh();
  }

  public function refresh():Void
  {
    sort(SortUtil.byZIndex, FlxSort.ASCENDING);
  }

  public function applyExitMovers(?exitMovers:FreeplayState.ExitMoverData, ?exitMoversCharSel:FreeplayState.ExitMoverData):Void
  {
    if (exitMovers == null)
    {
      exitMovers = _exitMovers;
    }
    else
    {
      _exitMovers = exitMovers;
    }

    if (exitMovers == null) return;

    if (exitMoversCharSel == null)
    {
      exitMoversCharSel = _exitMoversCharSel;
    }
    else
    {
      _exitMoversCharSel = exitMoversCharSel;
    }

    if (exitMoversCharSel == null) return;

    exitMovers.set([newAlbumArt, difficultyStars],
      {
        x: FlxG.width,
        speed: 0.4,
        wait: 0
      });

    exitMoversCharSel.set([newAlbumArt, difficultyStars],
      {
        y: -175,
        speed: 0.8,
        wait: 0.1
      });
  }

  var titleTimer:Null<FlxTimer> = null;

  public function playIntro():Void
  {
    albumTitle.visible = false;
    newAlbumArt.visible = true;
    newAlbumArt.playAnimation('intro', true);

    difficultyStars.visible = false;
    new FlxTimer().start(0.75, function(_) {
      showTitle();
      showStars();
      albumTitle.animation.play('switch');
    });
  }

  public function skipIntro():Void
  {
    newAlbumArt.playAnimation('switch', true);
    albumTitle.animation.play('switch');
  }

  public function showTitle():Void
  {
    albumTitle.visible = true;
  }

  public function buildAlbumTitle(assetKey:String):Void
  {
    if (albumTitle != null)
    {
      remove(albumTitle);
      albumTitle = null;
    }

    albumTitle = FunkinSprite.createSparrow((FlxG.width - 355) - MobileScaleMode.gameNotchSize.x, 500, assetKey);
    albumTitle.visible = albumTitle.frames != null && newAlbumArt.visible;
    albumTitle.animation.addByPrefix('idle', 'idle0', 24, true);
    albumTitle.animation.addByPrefix('switch', 'switch0', 24, false);
    add(albumTitle);

    albumTitle.animation.finishCallback = (function(name) {
      if (name == 'switch') albumTitle.animation.play('idle');
    });
    albumTitle.animation.play('idle');

    if (_exitMovers != null) _exitMovers.set([albumTitle],
      {
        x: FlxG.width,
        speed: 0.4,
        wait: 0
      });

    if (_exitMoversCharSel != null) _exitMoversCharSel.set([albumTitle],
      {
        y: -190,
        speed: 0.8,
        wait: 0.1
      });
  }

  public function setDifficultyStars(?difficulty:Int):Void
  {
    if (difficulty == null) return;
    difficultyStars.difficulty = difficulty;
  }

  public function showStars():Void
  {
    difficultyStars.visible = true; 
    difficultyStars.flameCheck();
  }
}
